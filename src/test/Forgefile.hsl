#
#                               Copyright (C) 2015 by Rafael Santiago
#
# This is a free software. You can redistribute it and/or modify under
# the terms of the GNU General Public License version 2.
#
#
include Toolsets.hsl
include ../Forgeutils.hsl
include ~/fsutil.hsl

local var sources type list;
local var includes type list;
local var cflags type list;
local var libraries type list;
local var ldflags type list;

local var toolset_name type string;

cutest-test.preloading() {
    $toolset_name = get_toolset_basename() + "app";
}

project cutest-test : toolset $toolset_name : $sources, $includes, $cflags, $libraries, $ldflags, "cutest";

cutest-test.prologue() {
    hefesto.sys.echo("*** NOW RUNNING cutest's UNIT TESTS... wait...\n\n");

    $ldflags.add_item("../lib/libcutest.a");

    if (hefesto.sys.os_name() == "linux") {
        $ldflags.add_item("-ldl");
    } else if (hefesto.sys.os_name() == "freebsd") {
        $ldflags.add_item("-lexecinfo");
    } else if (hefesto.sys.os_name() == "minix") {
        $ldflags.add_item("-lexecinfo");
    } else if (hefesto.sys.os_name() == "sunos") {
        $cflags.add_item("-DNO_CUTEST_BACKTRACING");
    } else if (hefesto.sys.os_name() == "netbsd") {
        $ldflags.add_item("-lexecinfo");
    } else if (hefesto.sys.os_name() == "openbsd") {
        $ldflags.add_item("-lexecinfo");
    }

    if (hefesto.sys.os_name() != "windows") {
        if (isdir("/usr/local/include")) {
            $includes.add_item("/usr/local/include");
        }

        if (isdir("/usr/local/lib")) {
            $libraries.add_item("/usr/local/lib");
        }
    }

    $sources.ls(".*\\.c$");
}

cutest-test.epilogue() {
    if (hefesto.sys.last_forge_result() == 0) {
        var exit_code type int;
        if (hefesto.sys.os_name() != "windows") {
            $exit_code = hefesto.sys.run("bin/cutest --foo=bar --cutest-run-suite=suite_c,suite_a,suite_d");
        } else {
            $exit_code = hefesto.sys.run("bin\\cutest.exe --foo=bar --cutest-run-suite=suite_c,suite_a,suite_d");
        }
        if ($exit_code != 0) {
            hefesto.sys.echo("\n~~~~ UNIT TEST ERROR.\n\n");
            hefesto.project.abort($exit_code);
        }
        hefesto.sys.echo("\n*** BUILD SUCCESS.\n\n");
    }
}
