#
#                               Copyright (C) 2015 by Rafael Santiago
#
# This is a free software. You can redistribute it and/or modify under
# the terms of the GNU General Public License version 2.
#
#
include Toolsets.hsl
include Forgeutils.hsl
include ~/fsutil.hsl

local var sources type list;
local var includes type list;
local var cflags type list;
local var libraries type list;
local var ldflags type list;
local var libname type string;

local var toolset_name type string;

project cutest : toolset $toolset_name : $sources, $includes, $cflags, $libraries, $ldflags, $libname ;

cutest.preloading() {
    $toolset_name = get_toolset_basename() + "lib";
}

cutest.prologue() {
    var option type list;

    $option = hefesto.sys.get_option("build-skip");

    if ($option.index_of("cutest") > -1) {
        hefesto.sys.echo("BUILD INFO: cutest build was skipped.\n");
        hefesto.project.abort(0);
    }

    $sources.ls(".*\\.c$");

    $cflags = hefesto.sys.get_option("cflags");

    if (hefesto.sys.os_name() == "minix") {
        $cflags.add_item("-DHAS_NO_PTHREAD");
    } else if (hefesto.sys.os_name() == "sunos") {
        $cflags.add_item("-DNO_CUTEST_BACKTRACING");
    }

    $libname = "libcutest.a";

    if ($toolset_name == "clang-c-lib") {
        $cflags.add_item("-Wno-format");
    } else if ($toolset_name == "msvc-c-lib") {
		$cflags.add_item("-D_CRT_SECURE_NO_WARNINGS");
		$libname = "libcuteststaged.lib";
	}

    hefesto.sys.echo("*** NOW BUILDING " + $libname + "... wait...\n\n");

    if (hefesto.sys.os_name() != "windows") {
        if (isdir("/usr/local/include")) {
            $includes.add_item("/usr/local/include");
        }

        if (isdir("/usr/local/lib")) {
            $libraries.add_item("/usr/local/lib");
        }
    }
}

cutest.epilogue() {
    if (hefesto.sys.last_forge_result() == 0) {
        if ($toolset_name == "msvc-c-lib") {
            if (composite_libcutest() != 0) {
                hefesto.sys.echo("ERROR: Unable to composite libcutest.a\n");
                hefesto.project.abort(1);
            }
        }
        hefesto.sys.echo("\n*** libcutest.a WAS BUILT.\n\n");

        var exit_code type int;
        $exit_code = 1;
        if (hefesto.sys.cd("test")) {
            $exit_code = hefesto.sys.forge("cutest-test", "Forgefile.hsl", "--obj-output-dir=obj --bin-output-dir=bin");
            hefesto.sys.cd("..");
        }
        if ($exit_code != 0) {
            hefesto.project.abort($exit_code);
        } else {
            hefesto.sys.echo("\n*** ALL DONE.\n\n");
        }
    }
}
