#
#                               Copyright (C) 2015 by Rafael Santiago
#
# This is a free software. You can redistribute it and/or modify under
# the terms of the GNU General Public License version 2.
#
#
include Toolsets.hsl
include Forgeutils.hsl

local var sources type list;
local var includes type list;
local var cflags type list;
local var libraries type list;
local var ldflags type list;

local var toolset_name type string;

project cutest : toolset $toolset_name : $sources, $includes, $cflags, $libraries, $ldflags, "libcutest.a" ;

cutest.preloading() {
    $toolset_name = get_toolset_basename() + "lib";
}

cutest.prologue() {
    hefesto.sys.echo("*** NOW BUILDING libcutest.a... wait...\n\n");
    $sources.ls(".*\\.c$");
    $cflags = hefesto.sys.get_option("cflags");
    if ($toolset_name == "clang-c-lib") {
        $cflags.add_item("-Wno-format");
    }
}

cutest.epilogue() {
    if (hefesto.sys.last_forge_result() == 0) {
        hefesto.sys.echo("\n*** libcutest.a WAS BUILT.\n\n");
        var exit_code type int;
        $exit_code = 1;
        if (hefesto.sys.cd("test")) {
            $exit_code = hefesto.sys.forge("cutest-test", "Forgefile.hsl", "--obj-output-dir=obj --bin-output-dir=bin");
            hefesto.sys.cd("..");
        }
        if ($exit_code != 0) {
            hefesto.project.abort($exit_code);
        } else {
            hefesto.sys.echo("\n*** ALL DONE.\n\n");
        }
    }
}
