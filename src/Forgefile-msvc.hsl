#
#                               Copyright (C) 2015 by Rafael Santiago
#
# This is a free software. You can redistribute it and/or modify under
# the terms of the GNU General Public License version 2.
#
#
# Running this forge: hefesto --forgefiles=Forgefile-msvc.hsl --Forgefile-msvc-projects=cutest --link-model=shared
#
include ~/toolsets/vc/vc110-lib.hsl
include ~/toolsets/vc/vcvarsall/vcvarsall_110.hsl

var sources type list;
var includes type list;
var cflags type list;
var libraries type list;
var ldflags type list;

project cutest : toolset "vc110-lib" : $sources, $includes, $cflags, $libraries, $ldflags, "libcuteststaged.lib";

cutest.prologue() {
    hefesto.sys.echo("*** NOW BUILDING libcutest.lib... wait...\n\n");
    $sources.ls(".*\\.c$");
    $cflags = hefesto.sys.get_option("cflags");
    $ldflags = hefesto.sys.get_option("ldflags");
}

function composite_libcutest() : result type int {
    vc110_vcvarsall("");
    var msvcrtlib type string;
    var vspath type string;
    var compile_model type list;
    $vspath = hefesto.sys.env("VS110COMNTOOLS");
    $vspath = hefesto.sys.make_path($vspath, "..\\..\\VC\\lib");
    $compile_model = hefesto.sys.get_option("compile-model");
    $msvcrtlib = "msvcrt.lib";
    if ($compile_model.count() > 0) {
        var compile_model_data type string;
        $compile_model_data = $compile_model.item(0);
        if ($compile_model_data == "debug") {
            $msvcrtlib = "msvcrtd.lib";
        }
    }
    $msvcrtlib = "\"" + hefesto.sys.make_path($vspath, $msvcrtlib) + "\"";
    var composite_command type string;
    $composite_command = "lib.exe /OUT:lib\\libcutest.lib lib\\libcuteststaged.lib " + $msvcrtlib;
    var exit_code type int;
    $exit_code = hefesto.sys.run($composite_command);
    if ($exit_code == 0) {
        hefesto.sys.rm("lib\\libcuteststaged.lib");
    }
    result $exit_code;
}

cutest.epilogue() {
    if (hefesto.sys.last_forge_result() == 0) {
        hefesto.sys.echo("\n*** libcutest.lib WAS BUILT.\n\n");
        var exit_code type int;
        $exit_code = composite_libcutest();
        if ($exit_code != 0) {
            hefesto.sys.echo("ERROR: libcutest composition error!\n");
            hefesto.project.abort(1);
        }        
        $exit_code = 1;
        if (hefesto.sys.cd("test")) {
            var test_forge_options type string;
            $test_forge_options = "--obj-output-dir=obj --bin-output-dir=bin --link-model=static";
            var compile_model_option type list;
            $compile_model_option = hefesto.sys.get_option("compile-model");
            if ($compile_model_option.count() > 0) {
                var compile_model type string;
                $compile_model = $compile_model_option.item(0);
                $test_forge_options = $test_forge_options + " --compile-model=" + $compile_model;
            }
            $exit_code = hefesto.sys.forge("cutest-test", "Forgefile-msvc.hsl", $test_forge_options);
            hefesto.sys.cd("..");
        }
        if ($exit_code != 0) {
            hefesto.project.abort($exit_code);
        } else {
            hefesto.sys.echo("\n*** ALL DONE.\n\n");
        }
    }
}
